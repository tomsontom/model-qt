grammar org.ufacekit.qt.idl.dsl.QIdl with org.eclipse.xtext.xbase.Xbase

generate qIdl "http://www.ufacekit.org/qt/idl/dsl/QIdl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

QtIdlTyp:
	element=( QtBuitinType | QtClass | QtNamespace | QtTypeDef );

QtNamespace:
	'namespace' name=ID '{'
		( 
			namespaces+=QtNamespace | 
			enums+=QtEnum | 
			flags+=QtFlags | 
			classes+=QtClass | 
			operations+=QtOperation |
			typedef+=QtTypeDef
		)*
	'}'
;

QtType:
	QtEnum | QtFlags | QtClass | QtBuitinType | QtTypeDef | QtTemplateParam;

QtOperation:
	QtFunction | QtSignal | QtSlot;

QtClass:
	'class' name=ID ('<' (templateTypes+=QtTemplateParam) ( ',' templateTypes+=QtTemplateParam )* '>')? ('extends' superTypes+=QtClassTypeRef (',' superTypes+=QtClassTypeRef)*)? '{'
		( 
			enums+=QtEnum |
			flags+=QtFlags |
			classes+=QtClass |
			properties+=QtProperty |
			constructors+=QtConstructor |
			operations+=QtOperation |
			typedef+=QtTypeDef
		)*
	'}';
	
QtTemplateParam:
	name=ID // ( 'extends' (typeRef+=[QtTypeRef] | ('&' | typeRef+=[QtTypeRef])* ) )	
;

QtTypeDef:
	'typedef' name=ID ':' typeRef=QtTypeRef ';'
;

QtConstructor:
	'constructor' {QtConstructor} '(' (params+=QtParameter (',' params+=QtParameter)*)? ');';

QtProperty:
	'property' type=QtValue ';';

QtEnum:
	'enumeration' name=ID '{'
		values+=QtEnumValue*
	'}';

QtFlags:
	'flags' name=ID ':' enumeration=[QtEnum] ';';

QtEnumValue:
	name=ID '=' enumMask=QtEnumValueMask;

QtEnumValueMask:
	( (value+=[QtEnumValue|QualifiedName] | numericValue+=LONG) ('|' (value+=[QtEnumValue|QualifiedName] | numericValue+=LONG) )* );

QtBuitinType:
	'builtin' name=ID ':' value=JvmArgumentTypeReference;

QtFunction:
	visibility=Visibility const='const'? virtual=Virtuality? owner=Owner 'function' name=ID '(' (params+=QtParameter (',' params+=QtParameter)*)? ')' ':' returnValue=QtReturnValue ';';

QtValue:
	const='const'? valueType=Type? typeRef=QtTypeRef name=ID;

//QtTypeRef returns QtParameterizedTypeRef:
//	QtParameterizedTypeRef
//;

QtTypeRef: 
	type=[QtType|QualifiedName]  ('<' (templateTypes+=QtTypeRef) ( ',' templateTypes+=QtTypeRef )* '>')?	
;

QtClassTypeRef: 
	type=[QtClass|QualifiedName]  ('<' (templateTypes+=QtTypeRef) ( ',' templateTypes+=QtTypeRef )* '>')?	
;

QtParameter:
	value=QtValue ('=' (defaultInt=INT | defaultEnum=[QtEnumValue]) )?;

QtReturnValue:
	const='const'? valueType=Type? typeRef=QtTypeRef;

QtSignal:
	'signal' name=ID '(' (params+=QtParameter (',' params+=QtParameter)*)? ')' ':' returnValue=QtReturnValue ';'; 

QtSlot:
	'slot' name=ID '(' (params+=QtParameter (',' params+=QtParameter)*)? ')' ':' returnValue=QtReturnValue ';';

enum Virtuality:
	virtual | purevirtual;

enum Visibility: 
	public | protected;

enum Owner:
	member | class;

enum Type:
	pointer | reference;

terminal LONG returns ecore::ELong: '-'? ('0'..'9')+;